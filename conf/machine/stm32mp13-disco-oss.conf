#@TYPE: Machine
#@NAME: stm32mp13-disco-oss
#@DESCRIPTION: Configuration for STM32MP135f-DK board (OPTEE boot and SDcard only)
#@NEEDED_BSPLAYERS: layers/meta-openembedded/meta-oe layers/meta-openembedded/meta-python layers/meta-openembedded/meta-perl

include conf/machine/include/arm/armv7a/tune-cortexa7.inc

DEFAULTTUNE = "cortexa7thf-neon-vfpv4"

# Define specific familly common machine name
MACHINEOVERRIDES .= ":stm32mp13common:stm32mposscommon:stm32mp1common:stm32mpcommon"

STM32MP_SOC_NAME = "stm32mp13"

# =========================================================================
# MACHINE FEATURE
# =========================================================================
MACHINE_FEATURES = "ext2 ipsec nfs pci smbfs usbgadget usbhost vfat"
MACHINE_FEATURES += "wifi bluetooth splashscreen"
MACHINE_FEATURES += "watchdog"
MACHINE_FEATURES += "optee"
MACHINE_FEATURES += "nosmp"
MACHINE_FEATURES += "fw-update"

MACHINE_FEATURES += "autoresize initrd"
# =========================================================================
# Kernel
# =========================================================================
# Select kernel version
PREFERRED_PROVIDER_virtual/kernel = "linux-stm32mp-oss"
# Kernel image type
KERNEL_IMAGETYPE     =  "uImage"
KERNEL_ALT_IMAGETYPE = " zImage "

KERNEL_DEVICETREE = "stm32mp135f-dk.dtb"
SERIAL_CONSOLE = "115200 ttySTM0"
ST_KERNEL_LOADADDR ?= "0xC2000040"

KERNEL_CMDLINE ?= "console=ttySTM0,115200 rootwait rw root=PARTLABEL=rootfs"
# =========================================================================
# u-boot
# =========================================================================
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-stm32mp-oss"
PREFERRED_PROVIDER_u-boot = "u-boot-stm32mp-oss"

EXTRA_IMAGEDEPENDS += "virtual/bootloader"

UBOOT_CONFIG = "stm32mp135f-dk"
UBOOT_DEVICETREE = "stm32mp135f-dk.dtb"
# Define u-boot defconfig and binary to use for each UBOOT_CONFIG
UBOOT_CONFIG[stm32mp135f-dk] = "stm32mp13_disco_defconfig,,u-boot.bin"
# =========================================================================
# tf-a
# =========================================================================
PREFERRED_PROVIDER_virtual/trusted-firmware-a = "tf-a-stm32mp-oss"

EXTRA_IMAGEDEPENDS += "virtual/trusted-firmware-a"

# Add optionnaly tf-a
TF_A_DEVICETREE = "stm32mp135f-dk"
TF_A_DEVICETREE_SDCARD  = "stm32mp135f-dk"
TF_A_DEVICETREE_EMMC    = ""
TF_A_DEVICETREE_NAND    = ""
TF_A_DEVICETREE_NOR     = ""
#TF_A_DEVICETREE_SPINAND = ""

# =========================================================================
# OPTEE
# =========================================================================
PREFERRED_PROVIDER_virtual/optee-os = "optee-os-stm32mp-oss"

EXTRA_IMAGEDEPENDS += "virtual/optee-os"

# Map OPTEE configuration to device tree list
OPTEE_CONF = "stm32mp135f-dk "
OPTEE_CONF_EXTENDED = "stm32mp135f-dk:PLATFORM=stm32mp1-135F_DK"
# need to have a version of optee-client and optee-test aligned with optee-os
PREFERRED_PROVIDER_optee-client = "optee-client-oss"
PREFERRED_PROVIDER_optee-test = "optee-test-oss"

# =========================================================================
# MACHINE
# =========================================================================
IMAGE_CLASSES += " image_types-st"
IMAGE_FSTYPES  += " ext4 tar.xz stbootfs ${WKS_IMAGE_FSTYPES}"

# bluetooth
BLUETOOTH_LIST          = " linux-firmware-bluetooth-bcm4343 "
BLUETOOTH_LIST         += "${@bb.utils.contains('DISTRO_FEATURES','systemd',' bluetooth-suspend ','',d)}"
BLUETOOTH_IMAGE_INSTALL = "${@bb.utils.contains('MACHINE_FEATURES', 'bluetooth', '${BLUETOOTH_LIST}', '', d)} "

# Wifi
WIFI_LIST          = " linux-firmware-bcm43430 "
WIFI_IMAGE_INSTALL = "${@bb.utils.contains('MACHINE_FEATURES', 'wifi', '${WIFI_LIST}', '', d)} "

MACHINE_EXTRA_RDEPENDS += " \
    ${BLUETOOTH_IMAGE_INSTALL} \
    ${WIFI_IMAGE_INSTALL} \
    \
    kernel-image \
    kernel-modules \
    alsa-state-stm32mp1 alsa-state-script-oss \
    linux-firmware-bluetooth-bcm4343 \
    stm32mp-extlinux \
    ${@bb.utils.contains('MACHINE_FEATURES', 'splashscreen', 'u-boot-stm32mp-splash', '', d)} \
    efivar efibootmgr \
"
MACHINE_EXTRA_RRECOMMENDS += " \
    flashlayout \
    sdcard-raw-tools-native \
"

# =========================================================================
# Partition coming compatibility with meta-st-stm32mp and meta-st-opensstlinux
# =========================================================================
ST_BOOTFS = "0"
ST_VENDORFS = "0"
ST_USERFS = "0"

# =========================================================================
# WIC for sdcard raw image
# =========================================================================
#WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"
WKS_FILE_DEPENDS ?= " \
    virtual/bootloader \
    virtual/trusted-firmware-a \
    stm32mp-oss-doc-native \
    flashlayout \
    sdcard-raw-tools-native \
    e2fsprogs-native \
"

# for generated a WIC file, please uncomment the 2 following lines or add them to local.conf
WKS_IMAGE_FSTYPES = "wic wic.xz wic.bmap"
WKS_FILE += "sdcard-stm32mp135f-dk-optee-1GB.wks.in"

# Add more space on image
IMAGE_ROOTFS_EXTRA_SPACE = "131072"
