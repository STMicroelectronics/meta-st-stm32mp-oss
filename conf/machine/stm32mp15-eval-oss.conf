#@TYPE: Machine
#@NAME: stm32mp15-eval-oss
#@DESCRIPTION: Configuration for STM32MP157C-EV1 board (OPTEE boot and SDcard only)
#@NEEDED_BSPLAYERS: layers/meta-openembedded/meta-oe layers/meta-openembedded/meta-python layers/meta-openembedded/meta-perl

include conf/machine/include/tune-cortexa7.inc

DEFAULTTUNE = "cortexa7thf-neon-vfpv4"

#TUNE_FEATURES_remove_armv7ve = "cortexa7"

# Define specific familly common machine name
MACHINEOVERRIDES .= ":stm32mp15common:stm32mposscommon:stm32mp1common:stm32mpcommon"

STM32MP_SOC_NAME = "stm32mp15"

# =========================================================================
# MACHINE FEATURE
# =========================================================================
MACHINE_FEATURES = "ext2 ipsec nfs pci smbfs usbgadget usbhost vfat"
MACHINE_FEATURES += "splashscreen"
MACHINE_FEATURES += "watchdog"
MACHINE_FEATURES += "optee"

# =========================================================================
# Kernel
# =========================================================================
# Select kernel version
PREFERRED_PROVIDER_virtual/kernel = "linux-stm32mp-oss"
# Kernel image type
KERNEL_IMAGETYPE     =  "uImage"
KERNEL_ALT_IMAGETYPE = " zImage "

KERNEL_DEVICETREE = "stm32mp157c-dk2.dtb stm32mp157c-ev1.dtb"
SERIAL_CONSOLE = "115200 ttySTM0"
ST_KERNEL_LOADADDR ?= "0xC2000040"

KERNEL_CMDLINE ?= "console=ttySTM0,115200 rootwait rw root=PARTLABEL=rootfs"
# =========================================================================
# u-boot
# =========================================================================
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-stm32mp-oss"
PREFERRED_PROVIDER_u-boot = "u-boot-stm32mp-oss"

EXTRA_IMAGEDEPENDS += "virtual/bootloader"

UBOOT_CONFIG = "config-stm32mp157c-ev1"
UBOOT_DEVICETREE = "stm32mp157c-ev1.dtb"
# Define u-boot defconfig and binary to use for each UBOOT_CONFIG
UBOOT_CONFIG[config-stm32mp157c-ev1] = "stm32mp157c_ev1_trusted_defconfig,,u-boot.bin"

UBOOT_FRAGMENTS = "stm32mp157c-ev1"
UBOOT_FRAGMENTS[stm32mp157c-ev1] = "stm32mp157c_ev1_fragment.config"

# =========================================================================
# tf-a
# =========================================================================
PREFERRED_PROVIDER_virtual/trusted-firmware-a = "tf-a-stm32mp-oss"

EXTRA_IMAGEDEPENDS += "virtual/trusted-firmware-a"

# Add optionnaly tf-a
TF_A_DEVICETREE = "stm32mp157c-ev1"

# =========================================================================
# OPTEE
# =========================================================================
PREFERRED_PROVIDER_virtual/optee-os = "optee-os-stm32mp-oss"

EXTRA_IMAGEDEPENDS += "virtual/optee-os"

# Map OPTEE configuration to device tree list
OPTEE_CONF = "stm32mp157c-ev1"
OPTEE_CONF_EXTENDED = "stm32mp157c-ev1:PLATFORM=stm32mp1-157C_EV1"
# need to have a version of optee-client and optee-test aligned with optee-os
PREFERRED_PROVIDER_optee-client = "optee-client-oss"
PREFERRED_PROVIDER_optee-test = "optee-test-oss"

# =========================================================================
# MACHINE
# =========================================================================
IMAGE_FSTYPES  += " ext4 tar.xz"

MACHINE_EXTRA_RRECOMMENDS_append = " \
    kernel-image \
    kernel-devicetree \
    kernel-modules \
    alsa-state-stm32mp1 \
    linux-firmware-bluetooth-bcm4343 \
    u-boot-stm32mp-oss-extlinux \
    ${@bb.utils.contains('MACHINE_FEATURES', 'splashscreen', 'u-boot-stm32mp-splash', '', d)} \
    kernel-imagebootfs \
    \
    flashlayout \
    st-image-oss-bootfs \
    sdcard-raw-tools-native \
    \
    efivar efibootmgr \
"

# =========================================================================
# Partition coming compatibility with meta-st-stm32mp and meta-st-opensstlinux
# =========================================================================
ST_BOOTFS = "0"
ST_VENDORFS = "0"
ST_USERFS = "0"

# =========================================================================
# WIC for sdcard raw image
# =========================================================================
#WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"
WKS_FILE_DEPENDS ?= " \
    virtual/bootloader \
    virtual/trusted-firmware-a \
    stm32mp-oss-doc-native \
"

# for generated a WIC file, please uncomment the 2 following lines or add them to local.conf
IMAGE_FSTYPES += "wic wic.xz wic.bmap"
WKS_FILE += "sdcard-stm32mp157c-ev1-optee-1GB.wks.in"

# Add more space on image
IMAGE_ROOTFS_EXTRA_SPACE = "131072"
